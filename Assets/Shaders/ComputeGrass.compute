// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

RWStructuredBuffer<float4> _Positions;
uint _Resolution;
float _Density;

Texture2D<float4> _HeightMap;
SamplerState sampler_HeightMap;
float _HeightMapDisplacementStrength;

Texture2D<float4> _NoiseMapHeight;
SamplerState sampler_NoiseMapHeight;
float _HeightNoiseScaling;
#define MIN_GRASS_HEIGHT 0.5f;

void SetPosition(uint3 id)
{
    if (id.x >= _Resolution || id.y >= _Resolution)
    {
        return;
    }

    // convert position to texture space
    float2 uv = id.xy / _Density;
    uv.xy /= _Resolution / _Density;

    // sample heightmap
    const float displacement = _HeightMap.SampleLevel(sampler_HeightMap, uv, 0).r;
    
    // sample height data
    const float height = _NoiseMapHeight.SampleLevel(sampler_NoiseMapHeight, uv, 0).r * _HeightNoiseScaling + MIN_GRASS_HEIGHT;

    // calculate position
    float4 position = float4(id.x, 0, id.y, height);
    position.xz -= _Resolution * 0.5f; // adjust by half the resolution to center
    position.xz *= 1 / _Density;
    position.y += displacement * _HeightMapDisplacementStrength;
    
    _Positions[id.x + id.y * _Resolution] = position;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    SetPosition(id);
}
